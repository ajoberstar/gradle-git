buildscript {
	repositories {
		mavenCentral()
		maven { url 'https://oss.sonatype.org/content/groups/public/' }
	}
	dependencies { classpath 'org.ajoberstar:gradle-git:0.1.0-SNAPSHOT' }
}

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'gh-pages'

group = 'org.ajoberstar'
version = '0.1.0-SNAPSHOT'
github.repoUri = 'https://ajoberstar@github.com/ajoberstar/gradle-git.git'

repositories {
	mavenCentral()
}

dependencies {
	groovy localGroovy()
	compile gradleApi()
	compile 'org.eclipse.jgit:org.eclipse.jgit:1.2.0.201112221803-r'
}

github.ghpages.distribution {
	from(javadoc.outputs.files) {
		into 'docs/javadoc'
	}
	from(groovydoc.outputs.files) {
		into 'docs/groovydoc'
	}
}

task sourcesJar(type:Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type:Jar) {
	classifier = 'javadoc'
	from javadoc.outputs.files
}

task groovydocJar(type:Jar) {
	classifier = 'groovydoc'
	from groovydoc.outputs.files
}

artifacts {
	archives sourcesJar
	archives javadocJar
	archives groovydocJar
}

signing {
	sign configurations.archives
}

uploadArchives {
	repositories.mavenDeployer {
		name = 'Sonatype OSS'
		repository(url:'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
			authentication(userName:sonatypeUserName, password:sonatypePassword)
		}
		snapshotRepository(url:'https://oss.sonatype.org/content/repositories/snapshots') {
			authentication(userName:sonatypeUserName, password:sonatypePassword)
		}

		pom.project {
			name name
			description description
			url 'http://github.com/ajoberstar/gradle-git'
			licenses {
				license {
					name 'The Apache Software License, Version 2.0'
					url 'http://www.apache.org/licenses/LICENSE-2.0'
				}
			}
			developers {
				developer {
					id 'ajoberstar'
					name 'Andrew Oberstar'
					email 'andrew@ajoberstar.org'
				}
			}
			scm {
				connection 'git://github.com/ajoberstar/gradle-git.git'
				developerConnection 'git@github.com:ajoberstar/gradle-git.git'
				url 'http://github.com/ajoberstar/gradle-git'
			}
		}
	}
}
