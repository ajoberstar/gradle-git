buildscript {
	repositories {
		mavenCentral()
		maven { url 'https://oss.sonatype.org/content/groups/public/' }
	}
	dependencies {
		classpath 'org.ajoberstar:gradle-git:0.5.0'
		classpath 'org.ajoberstar:gradle-jacoco:0.2.0'
		classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.5.0'
	}
}

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'license'
apply plugin: 'jacoco'
apply plugin: 'github-pages'

group = 'org.ajoberstar'
description = 'Git plugins for Gradle'
version = '0.6.1'

sourceCompatibility = '1.5'

repositories {
	mavenCentral()
}

dependencies {
	compile localGroovy()
	compile gradleApi()
	compile 'org.eclipse.jgit:org.eclipse.jgit:2.2.0.201212191850-r'
	compile 'org.eclipse.jgit:org.eclipse.jgit.ui:2.2.0.201212191850-r'
	compile 'com.jcraft:jsch.agentproxy.jsch:0.0.6'
	compile 'com.jcraft:jsch.agentproxy.pageant:0.0.6'
	compile 'com.jcraft:jsch.agentproxy.sshagent:0.0.6'
	compile 'com.jcraft:jsch.agentproxy.usocket-jna:0.0.6'
	testCompile 'junit:junit:4.10'
	testCompile('org.spockframework:spock-core:0.7-groovy-1.8') { exclude group: 'org.codehaus.groovy' }
}

task sourcesJar(type:Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type:Jar) {
	classifier = 'javadoc'
	from javadoc.outputs.files
}

task groovydocJar(type:Jar) {
	classifier = 'groovydoc'
	from groovydoc.outputs.files
}

artifacts {
	archives sourcesJar
	archives javadocJar
	archives groovydocJar
}

signing {
	sign configurations.archives
}

uploadArchives {
	repositories.mavenDeployer {
		name = 'Sonatype OSS'
		repository(url:'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
			authentication(userName:sonatypeUserName, password:sonatypePassword)
		}
		snapshotRepository(url:'https://oss.sonatype.org/content/repositories/snapshots') {
			authentication(userName:sonatypeUserName, password:sonatypePassword)
		}
		
		beforeDeployment { signing.signPom it }

		pom.project {
			name project.name
			description project.description
			url 'http://github.com/ajoberstar/gradle-git'
			licenses {
				license {
					name 'The Apache Software License, Version 2.0'
					url 'http://www.apache.org/licenses/LICENSE-2.0'
				}
			}
			developers {
				developer {
					id 'ajoberstar'
					name 'Andrew Oberstar'
					email 'andrew@ajoberstar.org'
				}
			}
			scm {
				connection 'git://github.com/ajoberstar/gradle-git.git'
				developerConnection 'git@github.com:ajoberstar/gradle-git.git'
				url 'http://github.com/ajoberstar/gradle-git'
			}
		}
	}
}

license {
	header file('config/HEADER')
	strictCheck = true
	useDefaultMappings = false
	mapping 'groovy', 'SLASHSTAR_STYLE'
	mapping 'java', 'SLASHSTAR_STYLE'
}

tasks.withType(nl.javadude.gradle.plugins.license.License) {
	exclude '**/*.properties'
}

githubPages {
	repoUri = 'git@github.com:ajoberstar/gradle-git.git'
	pages {
		from 'src/gh-pages'
		from(javadoc.outputs.files) {
			into 'docs/javadoc'
		}
		from(groovydoc.outputs.files) {
			into 'docs/groovydoc'
		}
	}
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.6'
}
