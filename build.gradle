buildscript {
	repositories {
		mavenCentral()
		maven { url 'https://oss.sonatype.org/content/groups/public/' }
	}
	dependencies { classpath 'org.ajoberstar:gradle-git:0.2.3' }
}

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'github-pages'

group = 'org.ajoberstar'
description = 'Git plugins for Gradle'
version = '0.3.0-SNAPSHOT'

repositories {
	mavenCentral()
}

dependencies {
	groovy localGroovy()
	compile gradleApi()
	compile 'org.eclipse.jgit:org.eclipse.jgit:2.0.0.201206130900-r'
	compile 'org.eclipse.jgit:org.eclipse.jgit.ui:2.0.0.201206130900-r'
    testCompile( 'org.spockframework:spock-core:0.7-groovy-1.8' ) { exclude group: 'org.codehaus.groovy' }
    testCompile 'junit:junit:4.10'
}

githubPages {
	repoUri = 'git@github.com:ajoberstar/gradle-git.git'
	pages {
		from(javadoc.outputs.files) {
			into 'docs/javadoc'
		}
		from(groovydoc.outputs.files) {
			into 'docs/groovydoc'
		}
	}
}

task sourcesJar(type:Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type:Jar) {
	classifier = 'javadoc'
	from javadoc.outputs.files
}

task groovydocJar(type:Jar) {
	classifier = 'groovydoc'
	from groovydoc.outputs.files
}

artifacts {
	archives sourcesJar
	archives javadocJar
	archives groovydocJar
}

signing {
	sign configurations.archives
}

uploadArchives {
	repositories.mavenDeployer {
		name = 'Sonatype OSS'
		repository(url:'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
			authentication(userName:sonatypeUserName, password:sonatypePassword)
		}
		snapshotRepository(url:'https://oss.sonatype.org/content/repositories/snapshots') {
			authentication(userName:sonatypeUserName, password:sonatypePassword)
		}
		
		beforeDeployment { signing.signPom it }

		pom.project {
			name project.name
			description project.description
			url 'http://github.com/ajoberstar/gradle-git'
			licenses {
				license {
					name 'The Apache Software License, Version 2.0'
					url 'http://www.apache.org/licenses/LICENSE-2.0'
				}
			}
			developers {
				developer {
					id 'ajoberstar'
					name 'Andrew Oberstar'
					email 'andrew@ajoberstar.org'
				}
			}
			scm {
				connection 'git://github.com/ajoberstar/gradle-git.git'
				developerConnection 'git@github.com:ajoberstar/gradle-git.git'
				url 'http://github.com/ajoberstar/gradle-git'
			}
		}
	}
}
